buildscript {
	dependencies {
		classpath group: "com.gradle", name: "build-scan-plugin", version: "1.16"
		classpath group: "com.liferay", name: "com.liferay.gradle.plugins.change.log.builder", version: "1.1.1"
		classpath group: "com.liferay", name: "com.liferay.gradle.plugins.source.formatter", version: "2.3.293"
		classpath group: "nu.studer", name: "gradle-credentials-plugin", version: "1.0.4"
	}

	repositories {
		jcenter()

		maven {
			url "https://plugins.gradle.org/m2/"
		}

		maven {
			url "https://repository-cdn.liferay.com/nexus/content/groups/public"
		}
	}
}

apply plugin: "com.gradle.build-scan"
apply plugin: "com.liferay.change.log.builder"

task clean(type:Delete)

buildChangeLog {
	changeLogHeader = "BLADE Version ${project.version}"
	dirs = ["cli", "extensions", "gradle", "gradle-tooling", "libs"]
	ticketIdPrefixes = ["BLADE", "IDE", "LPS", "LRDOCS"]
}

buildScan {
	termsOfServiceUrl = "https://gradle.com/terms-of-service"
	termsOfServiceAgree = "yes"
}

clean {
	doFirst {
		String mavenLocalRepo = System.getProperty("maven.local.repo")

		if (mavenLocalRepo != null) {
			delete mavenLocalRepo
		}
	}
}

subprojects {
	repositories {
		maven {
			url "https://repository-cdn.liferay.com/nexus/content/groups/public"
		}
	}

	apply plugin: "com.liferay.source.formatter"
	apply plugin: "nu.studer.credentials"

	checkSourceFormatting {
		maxLineLength = 120
	}

	formatSource {
		maxLineLength = 120
	}

	group = "com.liferay.blade"

	plugins.withId("java") {
		repositories {
			flatDir {
				dirs "${rootProject.projectDir}/libs"
			}

			maven {
				url "https://repository-cdn.liferay.com/nexus/content/groups/public"
			}
		}

		sourceCompatibility = "1.8"
		targetCompatibility = "1.8"
	}

	plugins.withId("maven-publish") {
		publishing {
			String liferayNexusUsername = credentials.liferayNexusUsername
			String liferayNexusPassword = credentials.liferayNexusPassword
			String repoName = project.hasProperty("release") ? "liferay-public-releases" : "liferay-public-snapshots"

			repositories {
				maven {
					url "$rootProject.projectDir/mavenRepo"
				}

				maven {
					credentials {
						username liferayNexusUsername
						password liferayNexusPassword
					}

					url "https://repository.liferay.com/nexus/content/repositories/" + repoName
				}
			}
		}
	}
}
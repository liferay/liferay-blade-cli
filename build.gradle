import org.osgi.framework.Version

buildscript {
	dependencies {
		classpath group: "com.liferay", name: "com.liferay.gradle.plugins.change.log.builder", version: "1.1.1"
		classpath group: "com.liferay", name: "com.liferay.gradle.plugins.source.formatter", version: "latest.release"
		classpath group: "nu.studer", name: "gradle-credentials-plugin", version: "2.1"
		classpath group: "org.osgi", name: "osgi.core", version: "6.0.0"
	}

	repositories {
		maven {
			url "https://plugins.gradle.org/m2/"
		}

		maven {
			if (project.hasProperty("release") ) {
				url "https://repository.liferay.com/nexus/content/groups/public"
			}
			else {
				url "https://repository-cdn.liferay.com/nexus/content/groups/public"
			}
		}

		mavenCentral()

		jcenter()
	}
}

apply plugin: "com.liferay.change.log.builder"
apply plugin: "com.liferay.source.formatter"

tasks.register('clean', Delete)
tasks.register('dockerImageBuild', Exec)
tasks.register('dockerNexusDelete', Exec)
tasks.register('dockerNexusStart', Exec)
tasks.register('dockerNexusStop', Exec)
tasks.register('dockerPublishRelease', Exec)
tasks.register('dockerPublishSnapshots', Exec)

String userHome = System.getProperty("user.home")

String gradleHome = System.getProperty("gradle.user.home") ?: System.getenv("GRADLE_USER_HOME") ?: new File(userHome, ".gradle").absolutePath

File gradleCredentialsProperties = new File(gradleHome, "gradle.credentials.properties")

boolean release = project.hasProperty("release")

boolean localNexus = project.hasProperty("localNexus")

boolean remoteNexus = project.hasProperty("remoteNexus")

boolean skipTests = project.hasProperty("skipTests")

boolean skipScan = project.hasProperty("skipScan")

buildChangeLog {
	changeLogHeader = "BLADE Version ${project.version}"
	dirs = ["cli", "extensions", "gradle", "gradle-tooling", "libs"]
	ticketIdPrefixes = ["BLADE", "IDE", "LPS", "LRDOCS"]
	rangeStart = "23d6986650aac47c37e98a3a3aac5804de3970dd"
}

dockerImageBuild {
	List<String> args = ['docker', 'build', '--tag', 'liferay-blade-cli-build', '.']

	commandLine args
}

dockerNexusStart {
	executable "./nexus.sh"
}

dockerNexusDelete {
	commandLine "docker", "rm", "/local-nexus"
}

dockerNexusStop {
	commandLine "docker", "stop", "/local-nexus"
	finalizedBy 'dockerNexusDelete'
}

dockerPublishSnapshots {
	dependsOn dockerImageBuild

	if (!remoteNexus) {
		dependsOn 'dockerNexusStart'
		finalizedBy 'dockerNexusStop'
	}

	List<String> args = ['docker', 'run', '--rm', '--init']

	args.add('--network=host')

	args.addAll(['-v', "${userHome}/.gradle/wrapper:/root/.gradle/wrapper"])

	args.addAll(['-v', "${userHome}/.liferay/bundles:/root/.liferay/bundles"])

	if (gradleCredentialsProperties.exists()) {
		args.addAll(['-v', "${userHome}/.gradle/gradle.credentials.properties:/root/.gradle/gradle.encrypted.properties"])
	}

	args.add('liferay-blade-cli-build')

	if (remoteNexus) {
		args.addAll(['snapshots', '--remote'])
	}
	else {
		args.addAll(['snapshots', '--local'])
	}

	if (skipScan) {
		args.add('--skip-scan')
	}

	if (skipTests) {
		args.add('--skip-tests')
	}

	commandLine args
}

dockerPublishRelease {
	dependsOn dockerImageBuild

	List<String> args = ['docker', 'run', '--rm', '--init', '-v', "${userHome}/.gradle/wrapper:/root/.gradle/wrapper", "--network=host"]

	if (!remoteNexus) {
		dependsOn 'dockerNexusStart'
		finalizedBy 'dockerNexusStop'
	}

	if (gradleCredentialsProperties.exists()) {
		args.addAll(['-v', "${userHome}/.gradle/gradle.credentials.properties:/root/.gradle/gradle.encrypted.properties"])
	}

	args.addAll(['liferay-blade-cli-build', 'release'])

	if (remoteNexus) {
		args.add('--remote')
	}
	else {
		args.add('--local')
	}

	if (skipTests) {
		args.add('--skip-tests')
	}
	commandLine args
}

checkSourceFormatting {
	maxLineLength = 120
}

clean {
	doFirst {
		String mavenLocalRepo = System.getProperty("maven.local.repo")

		if (mavenLocalRepo != null) {
			delete mavenLocalRepo
		}
	}
}

formatSource {
	maxLineLength = 120
}

repositories {
	maven {
		if (release) {
			url "https://repository.liferay.com/nexus/content/groups/public"
		}
		else {
			url "https://repository-cdn.liferay.com/nexus/content/groups/public"
		}
	}

	if (!remoteNexus) {
		maven {
			allowInsecureProtocol = true
			url "http://localhost:8081/nexus/content/groups/public/"
		}
	}
}

subprojects {
	subproject ->
	group = "com.liferay.blade"

	pluginManager.withPlugin("java") {
		apply plugin: "com.liferay.source.formatter"
		apply plugin: "nu.studer.credentials"

		checkSourceFormatting {
			maxLineLength = 120
		}

		formatSource {
			maxLineLength = 120
		}

		sourceCompatibility = "1.8"
		targetCompatibility = "1.8"
	}

	tasks.withType(GenerateModuleMetadata).configureEach {
		enabled = false
	}

	pluginManager.withPlugin("maven-publish") {
		tasks.register('prepareRelease')
		tasks.register('prepareSnapshot')

		prepareRelease {
			doLast {
				File buildGradle = subproject.file("build.gradle")

				buildGradle.text = buildGradle.text.replaceAll(/version = "(.*)-SNAPSHOT"/) {
					all, version ->
					"version = \"$version\""
				}

				buildGradle.text = buildGradle.text.replaceAll(/name: "com.liferay.blade.extensions.maven.profile", version: "(.*)-SNAPSHOT"/) {
					all, version ->
					"name: \"com.liferay.blade.extensions.maven.profile\", version: \"$version\""
				}

				buildGradle.text = buildGradle.text.replaceAll(/name: "com.liferay.project.templates.js.theme", version: "(.*)-SNAPSHOT"/) {
					all, version ->
					"name: \"com.liferay.project.templates.js.theme\", version: \"$version\""
				}

				buildGradle.text = buildGradle.text.replaceAll(/name: "com.liferay.project.templates.js.widget", version: "(.*)-SNAPSHOT"/) {
					all, version ->
					"name: \"com.liferay.project.templates.js.widget\", version: \"$version\""
				}
			}
		}

		prepareSnapshot {
			doLast {
				File buildGradle = subproject.file("build.gradle")

				buildGradle.text = buildGradle.text.replaceAll(/version = "(.*)"/) {
					all, version ->
					Version currentVersion = new Version(version)

					Version newVersion = new Version(currentVersion.major, currentVersion.minor, currentVersion.micro + 1)

					"version = \"$newVersion-SNAPSHOT\""
				}

				buildGradle.text = buildGradle.text.replaceAll(/name: "com.liferay.blade.extensions.maven.profile", version: "(.*)"/) {
					all, version ->
					Version currentVersion = new Version(version)

					Version newVersion = new Version(currentVersion.major, currentVersion.minor, currentVersion.micro + 1)

					"name: \"com.liferay.blade.extensions.maven.profile\", version: \"${newVersion}-SNAPSHOT\""
				}

				buildGradle.text = buildGradle.text.replaceAll(/name: "com.liferay.project.templates.js.theme", version: "(.*)"/) {
					all, version ->
					Version currentVersion = new Version(version)

					Version newVersion = new Version(currentVersion.major, currentVersion.minor, currentVersion.micro + 1)

					"name: \"com.liferay.project.templates.js.theme\", version: \"${newVersion}-SNAPSHOT\""
				}

				buildGradle.text = buildGradle.text.replaceAll(/name: "com.liferay.project.templates.js.widget", version: "(.*)"/) {
					all, version ->
					Version currentVersion = new Version(version)

					Version newVersion = new Version(currentVersion.major, currentVersion.minor, currentVersion.micro + 1)

					"name: \"com.liferay.project.templates.js.widget\", version: \"${newVersion}-SNAPSHOT\""
				}
			}
		}

		publishing {
			String liferayNexusUsername = System.getenv("LIFERAY_NEXUS_USERNAME")
			String liferayNexusPassword = System.getenv("LIFERAY_NEXUS_PASSWORD")

			String repoName = release ? "liferay-public-releases" : "liferay-public-snapshots"
			String repoUrl = "https://repository.liferay.com/nexus/content/repositories/" + repoName

			if (liferayNexusUsername == null || liferayNexusPassword == null) {
				if (remoteNexus) {
					liferayNexusUsername = credentials.liferayNexusUsername
					liferayNexusPassword = credentials.liferayNexusPassword
				}
				else if (localNexus) {
					liferayNexusUsername = "admin"
					liferayNexusPassword = "admin123"

					if (release) {
						repoUrl = "http://localhost:8081/nexus/content/repositories/releases/"
					}
					else {
						repoUrl = "http://localhost:8081/nexus/content/repositories/snapshots/"
					}
				}
				else {
					liferayNexusUsername = ""
					liferayNexusPassword = ""
				}
			}

			repositories {
				maven {
					if (localNexus) {
						allowInsecureProtocol = true
					}

					credentials {
						username liferayNexusUsername.trim()
						password liferayNexusPassword.trim()
					}

					url repoUrl
				}
			}
		}
	}

	if (project.hasProperty("mavenLocal")) {
		repositories {
			mavenLocal()
		}
	}
}
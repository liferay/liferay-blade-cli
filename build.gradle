buildscript {
	dependencies {
		classpath group: "com.gradle", name: "build-scan-plugin", version: "1.16"
		classpath group: "com.liferay", name: "com.liferay.gradle.plugins.change.log.builder", version: "1.1.1"
		classpath group: "com.liferay", name: "com.liferay.gradle.plugins.source.formatter", version: "2.3.303"
		classpath group: "nu.studer", name: "gradle-credentials-plugin", version: "1.0.4"
	}

	repositories {
		jcenter()

		maven {
			url "https://plugins.gradle.org/m2/"
		}

		maven {
			if (project.hasProperty("release")) {
				url "https://repository.liferay.com/nexus/content/groups/public"
			}
			else {
				url "https://repository-cdn.liferay.com/nexus/content/groups/public"
			}
		}
	}
}

apply plugin: "com.gradle.build-scan"
apply plugin: "com.liferay.change.log.builder"
apply plugin: "com.liferay.source.formatter"

task buildInDocker(type:Exec)
task clean(type:Delete)

buildChangeLog {
	changeLogHeader = "BLADE Version ${project.version}"
	dirs = ["cli", "extensions", "gradle", "gradle-tooling", "libs"]
	ticketIdPrefixes = ["BLADE", "IDE", "LPS", "LRDOCS"]
}

buildScan {
	termsOfServiceUrl = "https://gradle.com/terms-of-service"
	termsOfServiceAgree = "yes"
}

checkSourceFormatting {
	maxLineLength = 120
}

clean {
	doFirst {
		String mavenLocalRepo = System.getProperty("maven.local.repo")

		if (mavenLocalRepo != null) {
			delete mavenLocalRepo
		}
	}
}

buildInDocker {
	LinkedList commands = new LinkedList <String> ();

	commands.addAll(['docker', 'build', '--tag', 'liferay-gradle', '.'])

	commandLine commands.toArray()

	commands.clear()

	commands.addAll(['docker', 'run', '--rm', '--init', '-p', '5004:5004', 'liferay-gradle'])

	if (project.hasProperty('gradleTasks')) {

		for (String gradleTask in gradleTasks.split(' ')) {
			commands.add(gradleTask)

		}
	}

	commandLine commands.toArray()
}

formatSource {
	maxLineLength = 120
}

repositories {
	maven {
		if (project.hasProperty("release")) {
			url "https://repository.liferay.com/nexus/content/groups/public"
		}
		else {
			url "https://repository-cdn.liferay.com/nexus/content/groups/public"
		}
	}
}

subprojects {
	group = "com.liferay.blade"

	plugins.withId("java") {
		apply plugin: "com.liferay.source.formatter"
		apply plugin: "nu.studer.credentials"

		checkSourceFormatting {
			maxLineLength = 120
		}

		formatSource {
			maxLineLength = 120
		}
		sourceCompatibility = "1.8"
		targetCompatibility = "1.8"
	}

	plugins.withId("maven-publish") {
		publishing {
			String liferayNexusUsername = credentials.liferayNexusUsername
			String liferayNexusPassword = credentials.liferayNexusPassword

			String repoName = project.hasProperty("release") ? "liferay-public-releases" : "liferay-public-snapshots"
			String repoUrl = "https://repository.liferay.com/nexus/content/repositories/" + repoName

			if (project.hasProperty("localNexus")) {
				liferayNexusUsername = "admin"
				liferayNexusPassword = "admin123"

				if (project.hasProperty("release")) {
					repoUrl = "http://localhost:8081/nexus/content/repositories/releases/"
				}
				else {
					repoUrl = "http://localhost:8081/nexus/content/repositories/snapshots/"
				}
			}

			repositories {
				maven {
					credentials {
						username liferayNexusUsername
						password liferayNexusPassword
					}

					url repoUrl
				}
			}
		}
	}
}
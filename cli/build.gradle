import de.undercouch.gradle.tasks.download.Download

buildscript {
	dependencies {
		classpath group: "biz.aQute.bnd", name: "biz.aQute.bnd.gradle", version: "5.3.0"
		classpath group: "de.undercouch", name: "gradle-download-task", version: "5.4.0"
	}

	repositories {
		maven {
			if (project.hasProperty("release")) {
				url "https://repository.liferay.com/nexus/content/groups/public"
			}
			else {
				url "https://repository-cdn.liferay.com/nexus/content/groups/public"
			}
		}
	}
}

apply plugin: "biz.aQute.bnd.builder"
apply plugin: "java-library"
apply plugin: "maven-publish"

task bladeExtensionsVersions
task cliSourcesJar(type: Jar)
task cliTestJar(type: Jar, dependsOn: testClasses)
task copyMavenProfileJar(type: Copy)
task createToolingZip(type:Zip)
task createWrapperZip(type:Zip)
task downloadPortal(type: Download)
task downloadProductInfo(type: Download)
task unzipManifest(type: Copy)
task unzipPortal(type: Copy)

configurations {
	bladeExtensions {
		resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
		resolutionStrategy.cacheDynamicVersionsFor 0, 'seconds'
		transitive = false
	}

	testApi
}

artifacts {
	 testApi cliTestJar
}

clean.doFirst {
	delete "${buildDir}/tooling.zip", "${buildDir}/wrapper.zip"
}

cliSourcesJar {
	archiveClassifier.set("sources")
	from sourceSets.main.allJava
}

cliTestJar {
	dependsOn "unzipPortal"
	archiveClassifier.set("tests")
	from sourceSets.test.output
}

createWrapperZip {
	from "${rootProject.projectDir}"
	archiveFileName.set("wrapper.zip")
	destinationDirectory.set(new File("${buildDir}"))
	include "gradle/**"
	include "gradlew*"

	finalizedBy("processZipsResources")
}

dependencies {
	api group: "biz.aQute.bnd", name: "biz.aQute.bndlib", version: "5.3.0"
	api group: "com.liferay", name: "com.liferay.gogo.shell.client", version: "1.0.0"
	api group: "com.liferay", name: "com.liferay.project.templates", version: "5.0.257"
	api group: "commons-beanutils", name: "commons-beanutils", version: "1.9.4"
	api group: "commons-io", name: "commons-io", version: "2.6"
	api group: "commons-lang", name: "commons-lang", version: "2.6"
	api group: "org.apache.ant", name: "ant", version: "1.10.7"
	api group: "org.apache.commons", name: "commons-compress", version: "1.18"
	api group: "org.apache.commons", name: "commons-configuration2", version: "2.7"
	api group: "org.apache.commons", name: "commons-text", version: "1.8"
	api group: "org.apache.httpcomponents", name: "httpclient", version: "4.5.13"
	api group: "org.apache.httpcomponents", name: "httpcore", version: "4.4.14"
	api group: "org.apache.maven", name: "maven-aether-provider", version: "3.3.9"
	api group: "org.apache.maven", name: "maven-settings", version: "3.3.9"
	api group: "org.apache.maven", name: "maven-settings-builder", version: "3.3.9"
	api group: "org.eclipse.aether", name: "aether-api", version: "1.0.2.v20150114"
	api group: "org.eclipse.aether", name: "aether-connector-basic", version: "1.0.2.v20150114"
	api group: "org.eclipse.aether", name: "aether-impl", version: "1.0.2.v20150114"
	api group: "org.eclipse.aether", name: "aether-spi", version: "1.0.2.v20150114"
	api group: "org.eclipse.aether", name: "aether-transport-classpath", version: "1.0.2.v20150114"
	api group: "org.eclipse.aether", name: "aether-transport-file", version: "1.0.2.v20150114"
	api group: "org.eclipse.aether", name: "aether-transport-http", version: "1.0.2.v20150114"
	api group: "org.eclipse.aether", name: "aether-transport-wagon", version: "1.0.2.v20150114"
	api group: "org.eclipse.aether", name: "aether-util", version: "1.0.2.v20150114"
	api group: "org.fusesource.jansi", name: "jansi", version: "1.17.1"
	api group: "org.gradle", name: "gradle-base-services-groovy", version: "5.6.4"
	api group: "org.gradle", name: "gradle-core", version: "5.6.4"
	api group: "org.gradle", name: "gradle-tooling-api", version: "5.6.4"
	api group: "org.json", name: "json", version: "20190722"
	api group: "org.jsoup", name: "jsoup", version: "1.11.3"
	api group: "org.tukaani", name: "xz", version: "1.6"
	api name: "org.objectweb.asm-6.0.0"
	api name: "org.objectweb.asm.analysis-6.0.0"
	api name: "org.objectweb.asm.commons-6.0.0"
	api name: "org.objectweb.asm.tree-6.0.0"
	api name: "org.objectweb.asm.util-6.0.0"

	bladeExtensions group: "com.liferay.blade", name: "com.liferay.blade.extensions.maven.profile", version: "1.0.38-SNAPSHOT"
	bladeExtensions group: "com.liferay.blade", name: "com.liferay.project.templates.client.extension", version: "1.0.5-SNAPSHOT"
	bladeExtensions group: "com.liferay.blade", name: "com.liferay.project.templates.js.theme", version: "1.0.21-SNAPSHOT"
	bladeExtensions group: "com.liferay.blade", name: "com.liferay.project.templates.js.widget", version: "1.0.22-SNAPSHOT"

	compileOnly group: "org.osgi", name: "osgi.core", version: "6.0.0"
	compileOnly project(":gradle-tooling")

	testImplementation gradleTestKit()
	testImplementation group: "com.googlecode.java-diff-utils", name: "diffutils", version: "1.3.0"
	testImplementation group: "junit", name: "junit", version: "4.12"
	testImplementation group: "net.diibadaaba.zipdiff", name: "zipdiff", version: "1.0"
	testImplementation group: "org.easymock", name: "easymock", version: "3.5.1"
	testImplementation group: "org.osgi", name: "osgi.core", version: "6.0.0"
	testImplementation group: "org.powermock", name: "powermock-api-easymock", version: "2.0.4"
	testImplementation group: "org.powermock", name: "powermock-classloading-xstream", version: "2.0.4"
	testImplementation group: "org.powermock", name: "powermock-module-junit4", version: "2.0.4"
	testImplementation group: "org.powermock", name: "powermock-module-junit4-rule", version: "2.0.4"
	testImplementation group: "org.zeroturnaround", name: "zt-process-killer", version: "1.9"
	testImplementation project(":gradle-tooling")
}

compileTestJava{
	dependsOn("unzipPortal")
}

downloadPortal {
	src "https://releases-cdn.liferay.com/portal/7.4.3.4-ga4/liferay-ce-portal-tomcat-7.4.3.4-ga4-20211020095956970.tar.gz"
	dest new File("${System.getProperty('user.home')}/.liferay/bundles", 'liferay-ce-portal-tomcat-7.4.3.4-ga4-20211020095956970.tar.gz')
	onlyIfNewer true
}

downloadProductInfo {
	src "https://releases-cdn.liferay.com/tools/workspace/.product_info.json"
	dest buildDir
	onlyIfNewer true
}

jar {
	dependsOn("unzipPortal")
	archiveFileName.set("blade.jar")
	from createToolingZip, createWrapperZip, downloadProductInfo
}

processResources {
	dependsOn("unzipPortal")
	from configurations.bladeExtensions
	from bladeExtensionsVersions
}

bladeExtensionsVersions {
	File bladeExtensionsVersionsFile = new File(buildDir, "blade-extensions-versions.properties")

	outputs.file(bladeExtensionsVersionsFile)

	Properties properties = new Properties()

	doFirst {
		configurations.bladeExtensions.resolvedConfiguration.resolvedArtifacts.each {
			properties.setProperty(it.name, it.moduleVersion.id.version)
		}

	}

	doLast {
		properties.store(bladeExtensionsVersionsFile.newWriter(), null)
	}
}

project(":gradle-tooling").afterEvaluate {
	tooling ->
	createToolingZip {
		dependsOn ":gradle-tooling:jar"
		archiveFileName.set("tooling.zip")
		destinationDirectory.set(new File("${buildDir}"))
		from tooling.jar.archivePath

		finalizedBy("processZipsResources")
	}
}

publishing {
	publications {
		cliJar(MavenPublication) {
			artifactId = "com.liferay.blade.cli"

			pom.withXml {
				Node pom = asNode()
				pom.remove(pom.get("dependencies"))
			}

			from components.java

			artifact cliSourcesJar
			artifact cliTestJar
		}
	}
}

String repoHost = "repository-cdn.liferay.com"

if (project.hasProperty("release") || project.hasProperty("snapshots")) {
	repoHost = "repository.liferay.com"
}

repositories {
	flatDir {
		dirs "${rootProject.projectDir}/libs"
	}

	if (project.hasProperty("localNexus")) {
		maven {
			allowInsecureProtocol = true
			url "http://localhost:8081/nexus/content/groups/public/"
		}
	}

	jcenter()

	maven {
		url "https://${repoHost}/nexus/content/repositories/liferay-public-releases/"
	}

	if (!project.hasProperty("release")) {
		maven {
			url "https://${repoHost}/nexus/content/repositories/liferay-public-snapshots/"
		}
	}

	maven {
		url "https://repo.gradle.org/gradle/libs-releases-local/"
	}

	mavenCentral()
}

sourceSets {
	zips {
		resources {
			srcDir "${buildDir}"
			include "tooling.zip"
			include "wrapper.zip"
		}
	}
	test {
		runtimeClasspath += sourceSets.zips.output
	}
}

test {
	dependsOn jar, unzipManifest, unzipPortal

	systemProperty "bladeJarPath", jar.archivePath
	systemProperty "buildDir", "${buildDir}"
	systemProperty "liferay.home", "${buildDir}/liferay-ce-portal-7.4.3.4-ga4"
	systemProperty "jna.nosys", "true"
	systemProperty "testOutputPath", sourceSets.test.output.classesDirs.singleFile.absolutePath

	project(":extensions:bad-command").afterEvaluate {
		project ->
		dependsOn project.jar
		systemProperty "badCommandJarFile", project.jar.archivePath
	}

	project(":extensions:sample-command").afterEvaluate {
		project ->
		dependsOn project.jar
		systemProperty "sampleCommandJarFile", project.jar.archivePath
	}

	project(":extensions:sample-profile").afterEvaluate {
		project ->
		dependsOn project.jar
		systemProperty "sampleProfileJarFile", project.jar.archivePath
	}

	project(":extensions:sample-template").afterEvaluate {
		project ->
		dependsOn project.jar
		systemProperty "sampleTemplateJarFile", project.jar.archivePath
	}

	int availableProcessors = Runtime.runtime.availableProcessors()

	int forks = 1

	if (!debug && project.hasProperty("parallel")) {
		if (availableProcessors > 4) {

			forks = availableProcessors - 2
		}
		else if (availableProcessors > 2) {

			forks = availableProcessors - 1
		}
	}

	maxParallelForks = forks
}

unzipManifest {
	dependsOn jar

	from zipTree(jar.archivePath)
	into sourceSets.main.output.classesDirs.singleFile
	include '/META-INF/MANIFEST.MF'

	finalizedBy("compileTestJava")
}

processZipsResources{
	dependsOn("unzipPortal")
}

unzipPortal {
	dependsOn downloadPortal
	from tarTree(resources.gzip(downloadPortal.dest))
	into buildDir

	finalizedBy("processZipsResources")
}

version = "5.0.1-SNAPSHOT"

apply from: "blade-jar-smoke-tests.gradle"
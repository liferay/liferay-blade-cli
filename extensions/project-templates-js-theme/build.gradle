buildscript {
	dependencies {
		classpath group: "biz.aQute.bnd", name: "biz.aQute.bnd.gradle", version: "5.3.0"
	}

	repositories {
		maven {
			if (project.hasProperty("release")) {
				url "https://repository.liferay.com/nexus/content/groups/public"
			}
			else {
				url "https://repository-cdn.liferay.com/nexus/content/groups/public"
			}
		}
	}
}

apply plugin: "biz.aQute.bnd.builder"
apply plugin: "java-library"
apply plugin: "maven-publish"

dependencies {
	compileOnly group: "com.liferay", name: "com.liferay.project.templates.extensions", version: "1.0.123"
	compileOnly group: "org.apache.maven.archetype", name: "archetype-common", version: "2.4"
	compileOnly project(":cli")

	testImplementation gradleTestKit()
	testImplementation group: "junit", name: "junit", version: "4.12"
	testImplementation project(":cli")
	testImplementation project(path: ":cli", configuration: "testApi")
}

compileJava{
	dependsOn(":cli:unzipPortal")
	dependsOn(":cli:unzipManifest")
}

jar{
	dependsOn(":cli:unzipPortal")

	archiveBaseName.set("com.liferay.project.templates.js.theme")
}

publishing {
	publications {
		mavenProfileJar(MavenPublication) {
			artifactId = jar.baseName

			from components.java
		}
	}
}

test {
	dependsOn jar

	doFirst {
		systemProperty 'jsThemeTemplateJarFile', jar.archivePath
	}
}

version = "1.0.21-SNAPSHOT"